apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'de.mannodermaus.android-junit5'


android {

    compileSdkVersion 30
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "ff.time.keeper"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion 24 // Android 7.0
        targetSdkVersion 30
        versionCode gitVersionCode
        versionName gitVersionName
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        archivesBaseName = "$versionName"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        enable 'Interoperability'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // https://stackoverflow.com/a/47509465
    packagingOptions {
        exclude 'META-INF/LICENSE*'
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${variant.versionName}-${variant.getFlavorName()}.apk"
    }
}

dependencies {

    // abios updater
    implementation fileTree(include: ['*.aar', '*.jar'], dir: 'libs')
    implementation 'com.android.support:multidex:1.0.3'

    // annotation processors: must be included in every build.gradle file.
    annotationProcessor 'com.google.dagger:dagger-compiler:' + rootProject.daggerVersion
    annotationProcessor 'com.google.dagger:dagger-android-processor:' + rootProject.daggerVersion
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:" + rootProject.lifecycleVersion

    // unit tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.6.0"
    testImplementation "org.mockito:mockito-junit-jupiter:2.22.0"

    // android tests
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.0.0"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.0.0"

}
